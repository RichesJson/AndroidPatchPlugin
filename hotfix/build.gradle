buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
def currentVersion = '0.4.0'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifact "$buildDir/outputs/aar/${project.getName()}-release.aar"
            artifact sourceJar
            groupId 'com.xingshulin.singularity'
            artifactId 'hotfix-plugin'
            version currentVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An android library for applying hotfix downloaded form http://singularity.xingshulin.com')
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'android-app-hotfix'
        userOrg = 'xingshulin'
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/xingshulin/AndroidPatchPlugin.git'
        version {
            desc = 'android hotfix plugin v' + currentVersion
            name = currentVersion
            released = new Date()
        }
    }
}

